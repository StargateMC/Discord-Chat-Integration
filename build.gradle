buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
repositories {
    jcenter()
    maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }// Maybe needed for other mods in the future
    maven { url 'https://jitpack.io' }
}
version = "1.1.18"
group = "de.erdbeerbaerlp.dcintegration" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "dcintegration"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
 
minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
	makeObfSourceJar = false
    mappings = "snapshot_20171003"
}
configurations {
    embed
    compile.extendsFrom(embed)
}
reobf {
    shadowJar { mappingType = 'SEARGE' }
}
shadowJar {
    classifier = '1.12'
    configurations = [project.configurations.embed]
    relocate 'okhttp3', 'dcshadow.okhttp'
    relocate 'okio', 'dcshadow.okio'
    relocate 'club.minnced', 'dcshadow.club.minnced'
    relocate 'com.sun', 'dcshadow.com.sun'
    relocate 'com.iwebpp', 'dcshadow.com.iwebpp'
    relocate 'com.neovisionaries', 'dcshadow.com.neovisionaries'
    relocate 'gnu', 'dcshadow.gnu'
    relocate 'org.apache.commons.collections4', 'dcshadow.apache-collections4'
    relocate 'org.slf4j', 'dcshadow.slf4j'
    relocate 'org.json', 'dcshadow.org.json'
    relocate 'org.intellij', 'dcshadow.intellij'
    relocate 'org.jetbrains', 'dcshadow.jetbrains'
    relocate 'tomp2p', 'dcshadow.tomp2p'
    relocate 'com.fasterxml', 'dcshadow.fasterxml'
}
dependencies {
	embed("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
    embed("net.dv8tion:JDA:4.0.0_39")
    embed 'club.minnced:discord-webhooks:0.1.8'
    deobfCompile 'com.github.upcraftlp:votifier:master-SNAPSHOT'
}
tasks.build.dependsOn reobfShadowJar
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
}
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
jar {
    classifier = "1.12"
    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis(),
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        		'MixinConfigs': 'mixins.dcintegration.json',
        		"ForceLoadAsMod": "true",
        		'FMLCorePluginContainsFMLMod': 'true'
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = 'release'
            addGameVersion '1.12.2'
            addGameVersion '1.12.1'
            addGameVersion '1.12'
            mainArtifact(jar) {
                displayName = "DiscordIntegration-$version (MC 1.12)"
            }
            relations {
                optionalDependency 'emojicord'
                optionalDependency 'voting'
            }
        }
    }
}
tasks.curseforge.dependsOn(build)